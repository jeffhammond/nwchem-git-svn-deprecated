head	1.4;
access;
symbols;
locks
	D3Y133:1.4; strict;
comment	@c @;


1.4
date	2015.03.23.03.24.46;	author D3Y133;	state Exp;
branches;
next	1.3;

1.3
date	2015.03.17.06.41.04;	author D3Y133;	state Exp;
branches;
next	1.2;

1.2
date	2014.12.23.08.10.47;	author D3Y133;	state Exp;
branches;
next	1.1;

1.1
date	2014.12.21.08.31.55;	author D3Y133;	state Exp;
branches;
next	;


desc
@@


1.4
log
@Integrated DLASCALE to improve numerical stability of the gradient
evaluation.
@
text
@      FUNCTION WFN1_AD_DLANSY( NORM, UPLO, N, A, LDA, WORK )
      USE WFN1_AD1
      IMPLICIT NONE
#include "lapack/double/intf_wfn1_ad_dlassq.fh"
#include "lapack/double/intf_wfn1_ad_dlascale.fh"
      TYPE(WFN1_AD_DBLE) :: WFN1_AD_DLANSY
*
*  -- LAPACK auxiliary routine (version 3.2) --
*  -- LAPACK is a software package provided by Univ. of Tennessee,    --
*  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
*     November 2006
*
*     .. Scalar Arguments ..
      CHARACTER          NORM, UPLO
      INTEGER            LDA, N
*     ..
*     .. Array Arguments ..
      TYPE(WFN1_AD_DBLE) :: A( LDA, * ), WORK( * )
*     ..
*
*  Purpose
*  =======
*
*  DLANSY  returns the value of the one norm,  or the Frobenius norm, or
*  the  infinity norm,  or the  element of  largest absolute value  of a
*  real symmetric matrix A.
*
*  Description
*  ===========
*
*  DLANSY returns the value
*
*     DLANSY = ( max(abs(A(i,j))), NORM = 'M' or 'm'
*              (
*              ( norm1(A),         NORM = '1', 'O' or 'o'
*              (
*              ( normI(A),         NORM = 'I' or 'i'
*              (
*              ( normF(A),         NORM = 'F', 'f', 'E' or 'e'
*
*  where  norm1  denotes the  one norm of a matrix (maximum column sum),
*  normI  denotes the  infinity norm  of a matrix  (maximum row sum) and
*  normF  denotes the  Frobenius norm of a matrix (square root of sum of
*  squares).  Note that  max(abs(A(i,j)))  is not a consistent matrix norm.
*
*  Arguments
*  =========
*
*  NORM    (input) CHARACTER*1
*          Specifies the value to be returned in DLANSY as described
*          above.
*
*  UPLO    (input) CHARACTER*1
*          Specifies whether the upper or lower triangular part of the
*          symmetric matrix A is to be referenced.
*          = 'U':  Upper triangular part of A is referenced
*          = 'L':  Lower triangular part of A is referenced
*
*  N       (input) INTEGER
*          The order of the matrix A.  N >= 0.  When N = 0, DLANSY is
*          set to zero.
*
*  A       (input) DOUBLE PRECISION array, dimension (LDA,N)
*          The symmetric matrix A.  If UPLO = 'U', the leading n by n
*          upper triangular part of A contains the upper triangular part
*          of the matrix A, and the strictly lower triangular part of A
*          is not referenced.  If UPLO = 'L', the leading n by n lower
*          triangular part of A contains the lower triangular part of
*          the matrix A, and the strictly upper triangular part of A is
*          not referenced.
*
*  LDA     (input) INTEGER
*          The leading dimension of the array A.  LDA >= max(N,1).
*
*  WORK    (workspace) DOUBLE PRECISION array, dimension (MAX(1,LWORK)),
*          where LWORK >= N when NORM = 'I' or '1' or 'O'; otherwise,
*          WORK is not referenced.
*
* =====================================================================
*
*     .. Parameters ..
      DOUBLE PRECISION   ONE, ZERO
      PARAMETER          ( ONE = 1.0D+0, ZERO = 0.0D+0 )
*     ..
*     .. Local Scalars ..
      INTEGER            I, J
      TYPE(WFN1_AD_DBLE) :: ABSA, SCALE, SUM, VALUE1
*     ..
*     .. External Functions ..
      LOGICAL            LSAME
      EXTERNAL           LSAME
*     ..
*     .. Intrinsic Functions ..
c     INTRINSIC          ABS, MAX, SQRT
*     ..
*     .. Executable Statements ..
*
      IF( N.EQ.0 ) THEN
         VALUE1 = ZERO
      ELSE IF( LSAME( NORM, 'M' ) ) THEN
*
*        Find max(abs(A(i,j))).
*
         VALUE1 = ZERO
         IF( LSAME( UPLO, 'U' ) ) THEN
            DO 20 J = 1, N
               DO 10 I = 1, J
                  VALUE1 = MAX( VALUE1, ABS( A( I, J ) ) )
   10          CONTINUE
   20       CONTINUE
         ELSE
            DO 40 J = 1, N
               DO 30 I = J, N
                  VALUE1 = MAX( VALUE1, ABS( A( I, J ) ) )
   30          CONTINUE
   40       CONTINUE
         END IF
      ELSE IF( ( LSAME( NORM, 'I' ) ) .OR. ( LSAME( NORM, 'O' ) ) .OR.
     $         ( NORM.EQ.'1' ) ) THEN
*
*        Find normI(A) ( = norm1(A), since A is symmetric).
*
         VALUE1 = ZERO
         IF( LSAME( UPLO, 'U' ) ) THEN
            DO 60 J = 1, N
               SUM = ZERO
               DO 50 I = 1, J - 1
                  ABSA = ABS( A( I, J ) )
                  SUM = SUM + ABSA
                  WORK( I ) = WORK( I ) + ABSA
   50          CONTINUE
               WORK( J ) = SUM + ABS( A( J, J ) )
   60       CONTINUE
            DO 70 I = 1, N
               VALUE1 = MAX( VALUE1, WORK( I ) )
   70       CONTINUE
         ELSE
            DO 80 I = 1, N
               WORK( I ) = ZERO
   80       CONTINUE
            DO 100 J = 1, N
               SUM = WORK( J ) + ABS( A( J, J ) )
               DO 90 I = J + 1, N
                  ABSA = ABS( A( I, J ) )
                  SUM = SUM + ABSA
                  WORK( I ) = WORK( I ) + ABSA
   90          CONTINUE
               VALUE1 = MAX( VALUE1, SUM )
  100       CONTINUE
         END IF
      ELSE IF( ( LSAME( NORM, 'F' ) ) .OR. ( LSAME( NORM, 'E' ) ) ) THEN
*
*        Find normF(A).
*
         SCALE = ZERO
         SUM = ONE
c
c        Figure SCALE out first (improves the numerical stability of gradient
c        evaluation).
c
         if (lsame(uplo,'U')) then
            do j = 2, n
               call wfn1_ad_dlascale(j-1,a(1,j),1,scale)
            enddo
         else
            do j = 1, n-1
               call wfn1_ad_dlascale(n-j,a(j+1,j),1,scale)
            enddo
         endif
         call wfn1_ad_dlascale(n,a,lda+1,scale)
c
         IF( LSAME( UPLO, 'U' ) ) THEN
            DO 110 J = 2, N
               CALL WFN1_AD_DLASSQ( J-1, A( 1, J ), 1, SCALE, SUM )
  110       CONTINUE
         ELSE
            DO 120 J = 1, N - 1
               CALL WFN1_AD_DLASSQ( N-J, A( J+1, J ), 1, SCALE, SUM )
  120       CONTINUE
         END IF
         SUM = 2.0d0*SUM
         CALL WFN1_AD_DLASSQ( N, A, LDA+1, SCALE, SUM )
         VALUE1 = SCALE*SQRT( SUM )
      END IF
*
      WFN1_AD_DLANSY = VALUE1
      RETURN
*
*     End of DLANSY
*
      END
@


1.3
log
@Added interface declarations.
@
text
@d5 1
d157 15
@


1.2
log
@Fixing the usual suspects for AD.
@
text
@d4 1
a86 3
*     ..
*     .. External Subroutines ..
      EXTERNAL           DLASSQ
@


1.1
log
@Initial revision
@
text
@d3 1
d85 1
a85 1
      TYPE(WFN1_AD_DBLE) :: ABSA, SCALE, SUM, VALUE
d95 1
a95 1
      INTRINSIC          ABS, MAX, SQRT
d100 1
a100 1
         VALUE = ZERO
d105 1
a105 1
         VALUE = ZERO
d109 1
a109 1
                  VALUE = MAX( VALUE, ABS( A( I, J ) ) )
d115 1
a115 1
                  VALUE = MAX( VALUE, ABS( A( I, J ) ) )
d124 1
a124 1
         VALUE = ZERO
d136 1
a136 1
               VALUE = MAX( VALUE, WORK( I ) )
d149 1
a149 1
               VALUE = MAX( VALUE, SUM )
d167 1
a167 1
         SUM = 2*SUM
d169 1
a169 1
         VALUE = SCALE*SQRT( SUM )
d172 1
a172 1
      WFN1_AD_DLANSY = VALUE
@
